plugins {
 id "java"
 id "maven"
 id "eclipse"
 id "signing"
 id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.53"
 id "com.github.sherter.google-java-format" version "0.6"
 id "com.bmuschko.nexus" version "2.3.1"
 id "net.researchgate.release" version "2.6.0"
 id "com.jfrog.bintray" version "1.8.0"
}

task gitChangelogTask(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
 gitHubApi = "https://api.github.com/repos/tomasbjerre/violations-lib";
 gitHubToken = System.properties['GITHUB_OAUTH2TOKEN'];
 gitHubIssuePattern = "#([0-9]*)";
 file = new File("CHANGELOG.md");
 templateContent = new File('changelog.mustache').getText('UTF-8');
 removeIssueFromMessage = true
}

wrapper {
 gradleVersion = "4.3.1"
}

group = 'se.bjurr.violations'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
 mavenLocal()
 mavenCentral()
}

dependencies {
 testCompile 'junit:junit:4.12'
 testCompile 'org.assertj:assertj-core:2.3.0'
}

test {
 if (System.getProperty('DEBUG', 'false') == 'true') {
  jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=9009'
 }
}

eclipse {
 classpath {
  downloadSources = true
  downloadJavadoc = true
 }
}

sourceSets {
 main.java.srcDirs = ['src/main/java' ]
 test.java.srcDirs = ['src/test/java' ]
}

if (JavaVersion.current().isJava8Compatible()) {
 allprojects {
  tasks.withType(Javadoc) {
   options.addStringOption('Xdoclint:none', '-quiet')
  }
 }
}

modifyPom {
 project {
  name 'Violations Lib'
  description 'Library for parsing report files from static code analysis.'
  url 'https://github.com/tomasbjerre/violations-lib'
  inceptionYear '2015'
   scm {
   url 'https://github.com/tomasbjerre/violations-lib'
   connection 'scm:https://tomasbjerre@github.com/tomasbjerre/violations-lib.git'
   developerConnection 'scm:git://github.com/tomasbjerre/violations-lib.git'
  }

  licenses {
   license {
    name 'The Apache Software License, Version 2.0'
    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    distribution 'repo'
   }
  }

  developers {
   developer {
    id 'tomasbjerre'
    name 'Tomas Bjerre'
    email 'tomas.bjerre85@gmail.com'
   }
  }
 }
}

extraArchive {
 sources = true
 tests = true
 javadoc = true
}

compileJava.dependsOn 'googleJavaFormat'

bintray {
 user = System.getenv('BINTRAY_USER')
 key = System.getenv('BINTRAY_KEY')
 publish = true
 pkg {
  repo = 'maven'
  name = project.name
  desc = project.description
  licenses = ['Apache-2.0']
  vcsUrl = 'git@github.com:tomasbjerre/violations-lib.git'
  websiteUrl = 'https://github.com/tomasbjerre/violations-lib'
  issueTrackerUrl = 'https://github.com/tomasbjerre/violations-lib/issues'
  labels = ['violations', 'static code analysis', 'lint']
  publicDownloadNumbers = true
  githubRepo = 'tomasbjerre/violations-lib'
  githubReleaseNotesFile = 'CHANGELOG.md'
  version {
   name = version
   released  = new Date()
   mavenCentralSync {
    sync = true
    user = System.getenv('NEXUS_USERNAME')
    password = System.getenv('NEXUS_PASSWORD')
   }
  }
 }
}

afterReleaseBuild.dependsOn {
 [install, bintrayUpload]
}
