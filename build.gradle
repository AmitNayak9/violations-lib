buildscript {
 repositories {
  jcenter()
  maven {
   url "https://plugins.gradle.org/m2/"
  }
 }
 dependencies {
  classpath "gradle.plugin.se.bjurr.gitchangelog:git-changelog-gradle-plugin:1.48"
  classpath 'gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.6'
 }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'signing'
apply plugin: "se.bjurr.gitchangelog.git-changelog-gradle-plugin"
apply plugin: 'com.github.sherter.google-java-format'

task gitChangelogTask(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
 gitHubApi = "https://api.github.com/repos/tomasbjerre/violations-lib";
 gitHubToken = System.properties['GITHUB_OAUTH2TOKEN'];
 gitHubIssuePattern = "#([0-9]*)";
 file = new File("CHANGELOG.md");
 templateContent = new File('changelog.mustache').getText('UTF-8');
 removeIssueFromMessage = true
}

group = 'se.bjurr.violations'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
 mavenLocal()
 mavenCentral()
}

dependencies {
 testCompile 'junit:junit:4.12'
 testCompile 'org.assertj:assertj-core:2.3.0'
}

test {
 if (System.getProperty('DEBUG', 'false') == 'true') {
  jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=9009'
 }
}

eclipse {
 classpath {
  downloadSources = true
  downloadJavadoc = true
 }
}

sourceSets {
 main.java.srcDirs = ['src/main/java' ]
 test.java.srcDirs = ['src/test/java' ]
}

if (JavaVersion.current().isJava8Compatible()) {
 allprojects {
  tasks.withType(Javadoc) {
   options.addStringOption('Xdoclint:none', '-quiet')
  }
 }
}


uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: nexusUsername, password: nexusPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: nexusUsername, password: nexusPassword)
      }
pom.project {
  name 'Violations Lib'
  packaging 'jar'
  description 'Library for parsing report files from static code analysis.'
  url 'https://github.com/tomasbjerre/violations-lib'

  scm {
   url 'https://github.com/tomasbjerre/violations-lib'
   connection 'scm:https://tomasbjerre@github.com/tomasbjerre/violations-lib.git'
   developerConnection 'scm:git://github.com/tomasbjerre/violations-lib.git'
  }

  licenses {
   license {
    name 'The Apache Software License, Version 2.0'
    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    distribution 'repo'
   }
  }

  developers {
   developer {
    id 'tomasbjerre'
    name 'Tomas Bjerre'
    email 'tomas.bjerre85@gmail.com'
   }
  }
}

    }
  }
}

compileJava.dependsOn 'googleJavaFormat'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}


//afterReleaseBuild.dependsOn {
// [install, uploadArchives]
//}
